set term postscript enhanced color
set xlabel  "t(yr)"
extension = "ps"

unset yrange

set title "Period and Separation"
set ylabel  "P(s)"
set y2label "a(cm)"
set y2tics
set output data_path."/period".suffix.".".extension
plot data_path."/mdot.dat" u 1:3 lc 2 lt -1 axis x1y2 title "Separation" with lines,\
     data_path."/mdot.dat" u 1:4 lc 4 lt -1 title "Period" with lines

unset ylabel
unset log y
unset y2label
unset y2tics

set title "Overflow"
set ylabel "{/Symbol D}(Rsun)"
set output data_path."/overflow".suffix.".".extension
plot data_path."/mdot.dat" u 1:($7-$8) lt -1 lc 3 title "Overflow" with lines

set ylabel "Mass(M)"
unset log y
unset y2label
unset y2tics
set title "Mass of Components"
set output data_path."/mass".suffix.".".extension
plot data_path."/mdot.dat" u 1:6  lt -1 lc 3 title "Donor Mass" with lines,\
     data_path."/mdot.dat" u 1:10 lt -1 lc 2 title "Accretor Mass" with lines,\
     data_path."/mdot.dat" u 1:($6+$10) lt -1 lc 1 title "Total Mass" with lines

set ylabel "Ratio"
set title "Mass Ratio"
set output data_path."/q".suffix.".".extension
plot data_path."/mdot.dat" u 1:($6/$10) lt -1 lc 3 title "q" with lines,\
     data_path."/mdot.dat" u 1:20 lt 0 lc 3 title "q_s" with lines,\
     data_path."/mdot.dat" u 1:19 lt 0 lc 2 title "q_a" with lines

set ylabel "Efficiency"
set title "Accretion Efficiency"
set output data_path."/eff".suffix.".".extension
plot data_path."/mdot.dat" u 1:18 lt -1 lc 3 title "Beta" with lines

set ylabel "Timescale (yr)"
set log y
set format y "10^{%L}"
set title "Characteristic Timescales as a Function of Time"
set output data_path."/timescales".suffix.".".extension
plot data_path."/mdot.dat" u 1:21 lt -1 lc 1 title "GR Waves" with lines,\
     data_path."/mdot.dat" u 1:22 lt -1 lc 2 title "Separation" with lines,\
     data_path."/mdot.dat" u 1:23 lt -1 lc 3 title "Overflow" with lines,\
     data_path."/mdot.dat" u 1:24 lt -1 lc 4 title "Mass Transfer" with lines,\
     data_path."/mdot.dat" u 1:25 lt -1 lc 5 title "Mass Transfer Variation" with lines,\
     data_path."/mdot.dat" u 1:26 lt -1 lc 6 title "Relaxation Timescale" with lines,\
     data_path."/mdot.dat" u 1:($2/res_factor)  lt 0 lc 7 title "Time step x res_factor" with lines,\
     data_path."/mdot.dat" u 1:($4/year) lt -1 lc 8 title "Period" with lines
set format y "%g"

set ylabel "Radius (cm)"
unset log y
set title "Radius of Accretor, Disk or Direct Impact"
set output data_path."/disk_impact".suffix.".".extension
plot data_path."/mdot.dat" u 1:11 lt -1 lc 3 title "Accretor Radius" with lines,\
     data_path."/mdot.dat" u 1:12 lt -1 lc 1 title "Circularization Radius" with lines,\
     data_path."/mdot.dat" u 1:13 lt -3 lc 2 title "Minimum Approach" with lines

set title   "Mass Transfer Rate"
set ylabel  "M_{dot}(M/yr)"
set yrange [minmdot:maxmdot]
set log y
set format y "10^{%L}"
set output data_path."/mdot".suffix.".".extension
plot data_path."/mdot.dat" u 1:(-$5)  lc -1 lt -1  lw 1 title "M_{dot}" with lines,\
     data_path."/mdot.dat" u 1:(-$15) lc -1 lt  0  lw 3 title "M_{dotEq}" with lines,\
     data_path."/mdot.dat" u 1:16     lc -1 lt  2  lw 3 title "M_{dotEd}" with lines
unset log y
set format y "%g"

set title   "Optical Radius"
set ylabel  "R_{tau}(cm)"
set log y
set autoscale y
set format y "10^{%L}"
set output data_path."/rtau".suffix.".".extension
plot data_path."/mdot.dat" u 1:($28)  lc -1 lt -1  lw 1 title "Wind" with lines,\
     data_path."/mdot.dat" u 1:($31)  lc -1 lt -1  lw 1 title "Radiative" with lines,\
     data_path."/mdot.dat" u 1:($34)  lc -1 lt -1  lw 1 title "Bubble" with lines
unset log y
set format y "%g"

set title   "Effective Temperature"
set ylabel  "T_{eff}(K)"
set log y
set autoscale y
set format y "10^{%L}"
set output data_path."/teff".suffix.".".extension
plot data_path."/mdot.dat" u 1:($30)  lc -1 lt -1  lw 1 title "T_{eff}" with lines,\
     data_path."/mdot.dat" u 1:($33)  lc -1 lt -1  lw 1 title "T_{eff}" with lines,\
     data_path."/mdot.dat" u 1:($36)  lc -1 lt -1  lw 1 title "T_{eff}" with lines
unset log y
set format y "%g"
